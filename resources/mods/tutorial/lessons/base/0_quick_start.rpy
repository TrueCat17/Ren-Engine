init python:
	lessons.add_current('label, scene, show, text')

label quick_start:
	show rn 2 smile
	
	rn "Приве-ет! Рада, что ты открыл этот урок!"
	rn "Меня зовут Рен, и я буду твоим личным проводником в удивительный мир создания визуальных новелл. {w}И не только!"
	rn "А делать мы это будем на движке... Ren-Engine. Да-да, я нескромно назвала его в свою честь. Ну а кто бы на моём месте удержался?"
	
	rn "Этот небольшой тур - самая суть, выжимка из огромной документации."
	rn "Считай, что документация - это большая, серьёзная энциклопедия, а мы с тобой сейчас полистаем весёлый иллюстрированный путеводитель."
	rn "Но если захочешь копнуть глубже - ты всегда знаешь, где искать \"энциклопедию\"."
	
	show rn 4 smile with dissolve
	rn "Поехали!"
	
	show rn 2 smile with dissolve
	rn "Для начала нам нужно подготовить нашу стартовую площадку."
	rn "Просто скачай Лаунчер и распакуй его в ту папку, где будут рождаться твои будущие проекты."
	rn "Можно сказать, что мы закладываем основу нашего космолёта."
	
	menu:
		"Скопировать ссылку":
			$ set_clipboard_text('https://raw.githubusercontent.com/TrueCat17/RE-Launcher-upd-sys/master/zips/RE-Launcher.zip')
		"Спасибо, не надо":
			pass
	
	rn "Я даже могу подержать для тебя ссылку в буфере обмена."
	show rn 1 laugh with dissolve
	extend " Сервис!"
	
	rn "Теперь заходи в папку Лаунчера и смело его запускай. Он не кусается, обещаю."
	show rn 3 smile with dissolve
	rn "Там создай новый проект, выбери жанр \"Визуальная новелла\"... и... вуаля!"
	show rn 4 smile with dissolve
	rn "У тебя есть своя маленькая вселенная, готовая к заселению персонажами и историями."
	rn "Уже отсюда можно запустить игру, чтобы посмотреть на...."
	show rn 2 smile with dissolve
	rn "Впрочем, лучше открыть папку проекта и поисследовать его файлы, чем мы сейчас и займёмся."
	
	rn "Итак, сердце любой истории - это сценарий."
	rn "В нашем случае он живёт в файле [sts]resources/mods/std/main.rpy[ste]."
	rn "Не пугайся этого длинного пути, скоро он станет тебе родным. Кроме того, в Лаунчере есть кнопка, специально для его открытия."
	rn "Это обычный текстовый файл, так что можешь открыть его чуть ли не в любом блокноте."
	rn "Но я бы посоветовала что-то вроде Notepad++, он точно может работать с кодировкой UTF-8 и с переносами строк в Unix-стиле."
	show rn 3 smile with dissolve
	rn "Поддержка этих 2 вещей очень важна."
	
	show rn 3 smile:
		rn_moving 0.5 xpos 0.28
	
	python:
		code.show('''
			label start:
				scene bg entry
				
				show rn smile with dissolve
				rn "Visual Novell starts here."
				
				scene bg black with dissolve
		''')
	
	rn "Содержимое файла выглядит примерно вот так."
	rn "Если оно выглядит сильно иначе, то твой редактор, видимо, что-то не поддерживает."
	
	rn "Каждая пьеса начинается с первой сцены."
	rn "У нас для этого есть [sts]label start:[ste]. Это как табличка \"НАЧАЛО ЗДЕСЬ\"."
	rn "Всё, что будет происходить в игре, начинается отсюда."
	rn "И обрати внимание на отступы! Они играют важную роль - без них ничего не будет работать. {w}Так уж всё устроено."
	
	show rn 4 smile with dissolve
	rn "Видишь команды [sts]scene[ste] и [sts]show[ste]? Обе показывают картинки, но они с разным характером."
	rn "Команда [sts]scene bg entry[ste] - это как полная смена декораций. Она властно говорит: \"Так, все ушли со сцены! Сейчас будет новый фон!\"."
	rn "Сначала она всё очищает, а потом показывает одно, но большое фоновое изображение."
	show rn 2 smile with dissolve
	rn "А вот [sts]show rn smile with dissolve[ste] - это уже выход актёра."
	show rn 2 wink with dspr
	extend " То есть - меня!"
	show rn 2 smile with dspr
	rn "Эта команда аккуратно добавляет мой спрайт поверх фона."
	rn "Ключевое слово [sts]with[ste] и эффект [sts]dissolve[ste] после него - это то, как я появлюсь. В данном случае - плавно и быстро."
	extend " Эффектно, не правда ли?"
	
	show rn 4 smile with dissolve
	rn "Ну а строчка [sts]rn \"Текст.\"[ste] позволяет мне что-то сказать. Да, вот так просто."
	rn "Видишь [sts]rn[ste] в начале? Это мой короткий сценический псевдоним."
	rn "Когда движок его видит, он понимает, кто именно произносит фразу."
	rn "Ты, конечно же, сможешь создавать своих персонажей с их собственными именами!"
	rn "Смысл последней строчки, думаю, теперь очевиден, правда?"
	
	show rn 3 smile with dissolve
	rn "Кстати, об удобстве."
	
	$ code.hide()
	
	show rn 3 smile:
		rn_moving 0.5 xpos 0.5
	
	rn "Чтобы в папке проекта не было творческого беспорядка, мы всё аккуратно раскладываем по полочкам."
	rn "Всех персонажей, фоны, музыку и прочие штуки мы определяем в отдельных файлах. Найти их можно здесь: [sts]resources/mods/common/[ste]."
	show rn 2 smile with dissolve
	rn "Это как гримёрка для наших актёров и склад для реквизита."
	rn "Персонажи \"живут\" в [sts]characters.rpy[ste], спрайты - в [sts]sprites.rpy[ste] и так далее..."
	rn "Подобное разделение будет очень удобным, когда проект разрастётся, поверь!"
	
	show rn 1 smile with dissolve
	rn "И ещё один о-о-очень важный момент! Есть такая папка: [sts]Ren-Engine/rpy/[ste]. В ней лежат файлы самого движка."
	show rn 2 smile with dissolve
	rn "Это его мозг, его сердце, его душа!"
	rn "Ты можешь заглядывать туда, подсматривать, как всё устроено, изучать - я это даже поощряю!"
	rn "Но, пожалуйста, ничего там не меняй. Разве что кратковременно, ради исследовательских целей."
	rn "Иначе после обновления движка из Лаунчера все твои правки будут безжалостно удалены."
	rn "Давай договоримся: это - почти как музей, где можно смотреть, но нельзя трогать экспонаты руками. Хорошо?"
	
	rn "Ну вот! Первый, самый важный шаг - сделан!"
	rn "Теперь ты знаешь, где живёт движок, как создать проект и как сделать так, чтобы созданные персонажи могли прочитать реплики в твоей игре."
	rn "Это основа основ! Ну а о том, как этих персонажей создавать, я расскажу в следующих уроках!"
	
	rn "Я бы очень советовала тебе сейчас не бежать дальше, а немного поэкспериментировать."
	rn "Попробуй поменять мой текст. Может, даже заставишь меня сказать какую-нибудь глупость?"
	rn "Не бойся что-то сломать, это лучший способ научиться!"
	
	rn "А когда будешь готов, я буду ждать тебя в следующем уроке."
	rn "До скорой встречи!"
